IF
Die()
THEN
RESPONSE #100
Help()
Continue()
END

/*IF
Global("Bla_Lenscrew","ar1700",2)
Detect([PC])
Range(LastSeenBy(Myself),15)
NumTimesTalkedTo(0)
Allegiance(Myself,NEUTRAL)
THEN
RESPONSE #100
StartDialogNoSet(LastSeenBy(Myself))
END*/

IF
Global("Bla_Lenscrew","ar1700",9)
THEN
RESPONSE #100
EscapeAreaDestroy(20)
END

IF
!Allegiance(Myself,EVILCUTOFF)
Global("Bla_OgreHostile","ar1700",1)
THEN
RESPONSE #100
Enemy()
Help()
Continue()
END

IF
Global("bd_default_loc","locals",0)
THEN
RESPONSE #100
SaveObjectLocation("LOCALS","bd_default_loc",Myself)
Continue()
END

IF
!Allegiance(Myself,EVILCUTOFF)
AttackedBy([GOODCUTOFF],DEFAULT)
THEN
RESPONSE #100
SetGlobal("BD_Engaged","LOCALS",1)
SetGlobalTimer("BD_Rest","LOCALS",EIGHT_HOURS)
SetGlobalTimerRandom("bd_combat_timer","locals",THREE_MINUTES,FIVE_ROUNDS)
SetGlobalTimerRandom("bd_retreat_timer","locals",SEVEN_MINUTES,TEN_ROUNDS)
Enemy()
Help()
Continue()
END

IF
Allegiance(Myself,EVILCUTOFF)
Global("BD_Engaged","LOCALS",0)
OR(3)
AttackedBy([GOODCUTOFF],DEFAULT)
Range(NearestEnemyOf(Myself),30)
TookDamage()
THEN
RESPONSE #100
SetGlobal("BD_Engaged","LOCALS",1)
SetGlobalTimer("BD_Rest","LOCALS",EIGHT_HOURS)
SetGlobalTimer("BD_Help","LOCALS",ONE_ROUND)
SetGlobalTimerRandom("bd_combat_timer","locals",THREE_MINUTES,FIVE_ROUNDS)
SetGlobalTimerRandom("bd_retreat_timer","locals",SEVEN_MINUTES,TEN_ROUNDS)
Enemy()
Help()
Continue()
END

IF
Global("BD_Engaged","LOCALS",1)
!GlobalTimerNotExpired("BD_Rest","LOCALS")
THEN
RESPONSE #100
SetGlobalTimer("BD_Rest","LOCALS",EIGHT_HOURS)
SetGlobal("BD_CHCntg","LOCALS",0)
SetGlobal("BD_Cntg","LOCALS",0)
SetGlobal("BD_Seq","LOCALS",0)
SetGlobal("BD_MSeq","LOCALS",0)
SetGlobal("BD_Trig","LOCALS",0)
SetGlobal("BD_Precast","LOCALS",0)
SetGlobal("BD_DChrm","LOCALS",0)
ApplySpell(Myself,RESTORE_FULL_HEALTH)
Rest()
END

IF
Allegiance(Myself,EVILCUTOFF)
!GlobalTimerNotExpired("BD_Help","LOCALS")
OR(2)
Range(NearestEnemyOf(Myself),30)
AttackedBy([GOODCUTOFF],DEFAULT)
THEN
RESPONSE #100
SetGlobalTimer("BD_Help","LOCALS",ONE_ROUND)
SetGlobalTimerRandom("bd_combat_timer","locals",THREE_MINUTES,FIVE_ROUNDS)
SetGlobalTimerRandom("bd_retreat_timer","locals",SEVEN_MINUTES,TEN_ROUNDS)
Help()
Continue()
END

IF
Allegiance(Myself,EVILCUTOFF)
Global("BD_Engaged","LOCALS",0)
!Range(NearestEnemyOf(Myself),30)
OR(10)
Help([EVILCUTOFF])
Help(SecondNearest([EVILCUTOFF]))
Help(ThirdNearest([EVILCUTOFF]))
Help(FourthNearest([EVILCUTOFF]))
Help(FifthNearest([EVILCUTOFF]))
Help(SixthNearest([EVILCUTOFF]))
Help(SeventhNearest([EVILCUTOFF]))
Help(EighthNearest([EVILCUTOFF]))
Help(NinthNearest([EVILCUTOFF]))
Help(TenthNearest([EVILCUTOFF]))
LOS(LastHelp(Myself),30)
THEN
RESPONSE #100
SetGlobal("BD_Engaged","LOCALS",1)
SetGlobalTimer("BD_Rest","LOCALS",EIGHT_HOURS)
Help()
Continue()
END

IF
Global("BD_Engaged","LOCALS",1)
Allegiance(Myself,EVILCUTOFF)
!Range(NearestEnemyOf(Myself),30)
Help([ANYONE])
Allegiance(LastHelp(Myself),EVILCUTOFF)
TriggerOverride(LastHelp(Myself),LOS(LastSeenBy(Myself),30))
Allegiance(LastSeenBy(LastHelp(Myself)),GOODCUTOFF)
!GlobalTimerNotExpired("bd_respond_timer","locals")
THEN
RESPONSE #100
SetGlobalTimerRandom("bd_combat_timer","locals",FIVE_MINUTES,EIGHT_MINUTES)
SetGlobalTimerRandom("bd_retreat_timer","locals",NINE_MINUTES,TEN_ROUNDS)
SetGlobalTimer("bd_respond_timer","locals",TWO_ROUNDS)
MoveToObject(LastSeenBy(LastHelp(Myself)))
END

IF
Global("BD_Engaged","LOCALS",1)
Allegiance(Myself,EVILCUTOFF)
!Range(NearestEnemyOf(Myself),30)
Help([ANYONE])
Allegiance(LastHelp(Myself),EVILCUTOFF)
!Range(LastHelp(Myself),5)
!GlobalTimerNotExpired("bd_respond_timer","locals")
!GlobalTimerNotExpired("bd_respond2_timer","locals")
THEN
RESPONSE #100
SetGlobalTimerRandom("bd_combat_timer","locals",THREE_MINUTES,FIVE_ROUNDS)
SetGlobalTimerRandom("bd_retreat_timer","locals",SEVEN_MINUTES,TEN_ROUNDS)
SetGlobalTimer("bd_respond2_timer","locals",ONE_ROUND)
MoveToObject(LastHelp(Myself))
END

IF
Global("BD_Engaged","LOCALS",1)
Allegiance(Myself,EVILCUTOFF)
!Range(NearestEnemyOf(Myself),30)
!GlobalTimerNotExpired("bd_respond_timer","locals")
!GlobalTimerNotExpired("bd_respond2_timer","locals")
GlobalTimerNotExpired("bd_combat_timer","locals")
THEN
RESPONSE #100
RandomWalkContinuous()
END

IF
Global("BD_Engaged","LOCALS",1)
Allegiance(Myself,EVILCUTOFF)
!GlobalTimerNotExpired("bd_retreat_timer","locals")
!Range(NearestEnemyOf(Myself),30)
!NearSavedLocation(Myself,"LOCALSbd_default_loc",5)
THEN
RESPONSE #100
MoveToSavedLocation("bd_default_loc","LOCALS")
END

